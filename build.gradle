buildscript {
    ext {
        springBootVersion = '1.2.4.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.craigburke.gradle:karma-gradle:1.4.3"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:1.2"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'jacoco'
apply plugin: 'com.craigburke.karma'
apply plugin: 'org.sonarqube'
//apply plugin: 'pmd'
//apply plugin: 'jdepend'
//apply plugin: 'checkstyle'

apply from: 'gradle/versioning.gradle'

jar {
    baseName = project.name
    version = project.version.toString()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven { url 'http://repo.spring.io/release' }
    maven { url "http://repo.spring.io/libs-snapshot-local" }
    maven { url "http://repo.spring.io/libs-milestone-local" }
    maven { url "http://repo.spring.io/milestone" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-hateoas")
    compile("org.springframework.cloud:spring-cloud-starter-consul-all:1.0.0.M5")
    compile("org.codehaus.jackson:jackson-mapper-asl:1.9.13")
    compile("com.google.guava:guava:18.0")
    compile("org.apache.commons:commons-lang3:3.4")
    compile("org.springframework:spring-context-support:4.1.6.RELEASE")
    compile('io.springfox:springfox-swagger2:2.3.1')
    compile('io.springfox:springfox-swagger-ui:2.3.1')
    compile('io.swagger:swagger-project:1.5.6')

    testCompile('io.springfox:springfox-staticdocs:2.3.1')
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("junit:junit:4.12")
    testCompile("com.jayway.jsonpath:json-path:2.1.0")
    testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.0.RELEASE'
}

sourceSets {
    integrationTests {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
    docGeneration {
        java.srcDir file('src/docs/java')
        resources.srcDir file('src/docs/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests only.'
    group = 'verification'
    testClassesDir = sourceSets.integrationTests.output.classesDir
    classpath = sourceSets.integrationTests.runtimeClasspath
}

task generateDocs(type: Test) {
    description = 'Generates the documentation.'
    group = 'Documentation'
    testClassesDir = sourceSets.docGeneration.output.classesDir
    classpath = sourceSets.docGeneration.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

task javascriptTest(type: NpmTask) {
    args = ['run', 'single-test']
    description = 'Runs the javascript tests.'
    group = 'verification'
}

check.dependsOn javascriptTest
check.dependsOn integrationTest

karma {
    files = [
            'src/main/resources/static/components/angular/angular.js',
            'src/main/resources/static/components/angular-route/angular-route.js',
            'src/main/resources/static/components/angular-resource/angular-resource.js',
            'src/main/resources/static/components/angular-mocks/angular-mocks.js',
            'src/main/resources/static/app/**/*.js',
            'src/test/resources/static/spec/**/*.js'
    ]

    preprocessors = [
            'src/main/resources/static/app/**/*.js': 'coverage'
    ]

    reporters = ['progress', 'coverage']

    coverageReporter = [
            type: 'html',
            dir : 'build/coverage/'
    ]
}

